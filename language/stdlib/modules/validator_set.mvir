module ValidatorSet {
    import 0x0.Event;
    import 0x0.LibraAccount;
    import 0x0.ValidatorConfig;
    import 0x0.Vector;

    struct ValidatorInfo {
        addr: address,
        consensus_pubkey: bytearray,
        consensus_voting_power: u64,
        network_signing_pubkey: bytearray,
        network_identity_pubkey: bytearray,
    }

    struct ChangeEvent {
        new_validator_set: Vector.T<Self.ValidatorInfo>,
    }

    resource T {
        validators: Vector.T<Self.ValidatorInfo>,
        change_events: Event.Handle<Self.ChangeEvent>,
    }

    // This can only be invoked by the special validator set address, and only a single time.
    // Currently, it is invoked in the genesis transaction.
    public initialize() {
        // Only callable by the validator set address
        assert(get_txn_sender() == 0x1D8, 1);

        move_to_sender<T>(T {
            validators: Vector.empty<Self.ValidatorInfo>(),
            change_events: Event.new_event_handle<Self.ChangeEvent>(),
        });

        return;
    }

    // Return the size of the current validator set
    public size(): u64 acquires T {
        let validator_set_ref: &Self.T;

        validator_set_ref = borrow_global<T>(0x1D8);
        return Vector.length<Self.ValidatorInfo>(&move(validator_set_ref).validators);
    }

    // Return true if addr is a current validator
    public is_validator(addr: address): bool acquires T {
        let size: u64;
        let i: u64;
        let vector_ref: &Vector.T<Self.ValidatorInfo>;
        let info_ref: &Self.ValidatorInfo;

        vector_ref = &borrow_global<T>(0x1D8).validators;
        size = Vector.length<Self.ValidatorInfo>(copy(vector_ref));
        if (copy(size) == 0) {
            return false;
        }

        i = 0;
        info_ref = Vector.borrow<Self.ValidatorInfo>(copy(vector_ref), copy(i));
        while (copy(i) < copy(size)) {
            if (*&copy(info_ref).addr == copy(addr)) {
            // TODO: below doesn't work, looks like a compiler bug
            //if (*(&Vector.borrow<Self.ValidatorInfo>(copy(vector_ref), copy(i)).addr) == copy(addr)) {
                return true;
            }
            info_ref = Vector.borrow<Self.ValidatorInfo>(copy(vector_ref), copy(i));
            i = copy(i) + 1;
        }

        return false;
    }

    // TODO: Decide on access control policy. For now, we ensure that this is only callable from the
    // genesis txn. Obviously, we'll need a different policy once we support reconfiguration.
    add_validator(account_address: address) acquires T {
        let validator_set_ref: &mut Self.T;

        // A prospective validator must have a validator config resource
        assert(ValidatorConfig.has(copy(account_address)), 17);

        validator_set_ref = borrow_global_mut<T>(0x1D8);
        Vector.push_back<Self.ValidatorInfo>(
            &mut move(validator_set_ref).validators,
            ValidatorInfo {
                addr: move(account_address),
                consensus_pubkey: h"",
                // TODO: check for LIT, compute voting power based on LIT + stake
                consensus_voting_power: 1,
                network_signing_pubkey: h"",
                network_identity_pubkey: h"",
            }
        );

        return;
    }

    // Return true if the ValidatorInfo given as input is different than the ValidatorConfig
    // published at validator_info.addr + copies the differing fields. Aborts if there is no
    // ValidatorConfig at validator_info.addr
    public copy_validator_info(validator_info: &mut Self.ValidatorInfo): bool {
        let consensus_pubkey: bytearray;
        let network_signing_pubkey: bytearray;
        let network_identity_pubkey: bytearray;
        let config: ValidatorConfig.Config;
        let changed: bool;

        config = ValidatorConfig.config(*&copy(validator_info).addr);
        consensus_pubkey = ValidatorConfig.consensus_pubkey(&config);
        network_signing_pubkey = ValidatorConfig.network_signing_pubkey(&config);
        network_identity_pubkey = ValidatorConfig.network_identity_pubkey(&config);

        changed = false;
        if (&consensus_pubkey != &copy(validator_info).consensus_pubkey) {
            *&mut copy(validator_info).consensus_pubkey = move(consensus_pubkey);
            changed = true;
        }
       if (&network_signing_pubkey != &copy(validator_info).network_signing_pubkey) {
            *&mut copy(validator_info).network_signing_pubkey = move(network_signing_pubkey);
            changed = true;
        }
        if (&network_identity_pubkey != &copy(validator_info).network_identity_pubkey) {
            *&mut copy(validator_info).network_identity_pubkey = move(network_identity_pubkey);
            changed = true;
        }

        return move(changed);
    }

    // Trigger a reconfiguation the Libra system by:
    // (1) Computing a new validator set and storing it on chain
    // (2) Emitting an event containing new validator set, which will be passed to the executor
    // TODO: make  this private + only callable from block prologue
    public reconfigure() acquires T {
        let validator_set_ref: &mut Self.T;
        let vector_ref: &mut Vector.T<Self.ValidatorInfo>;
        let info_ref: &mut Self.ValidatorInfo;
        let i: u64;
        let len: u64;
        let changed: bool;

        // For now, this only supports a simple form of reconfiguration: allowing a fixed set of
        // validators to rotate their keys.
        // TODO: support adding and removing validators. Eventually, we will do this by computing
        // the new validator set from a larger list of candidate validators sorted by stake.
        validator_set_ref = borrow_global_mut<T>(0x1D8);
        vector_ref = &mut copy(validator_set_ref).validators;

        i = 0;
        len = Vector.length<Self.ValidatorInfo>(freeze(copy(vector_ref)));
        // assume(len > 0), since an empty validator set is nonsensical
        changed = false;
        info_ref = Vector.borrow_mut<Self.ValidatorInfo>(copy(vector_ref), copy(i));
        // check if each validator has rotated their keys, copy their new info and note the change
        // if so.
        loop {
            if (Self.copy_validator_info(move(info_ref))) {
                changed = true;
            }

            i = move(i) + 1;
            if (copy(i) >= copy(len)) {
                break;
            }
            info_ref = Vector.borrow_mut<Self.ValidatorInfo>(copy(vector_ref), copy(i));
        }

        if (move(changed)) {
            Event.emit_event<Self.ChangeEvent>(
                &mut move(validator_set_ref).change_events,
                ChangeEvent {
                    new_validator_set: *move(vector_ref),
                },
            );
            return;
        } else {
            return;
        }
    }

  // Get the address of the i'th validator.
  public get_ith_validator_address(i: u64): address acquires T {
      let len: u64;
      let validator_set: &Self.T;
      let addr: address;
      let peer: &Self.ValidatorInfo;

      validator_set = borrow_global<T>(0x1D8);
      len = Vector.length<Self.ValidatorInfo>(&copy(validator_set).validators);
      assert(copy(i) < move(len), 3);
      peer = Vector.borrow<Self.ValidatorInfo>(&move(validator_set).validators, move(i));
      addr = *(&move(peer).addr);

      return move(addr);
  }
}
