module ValidatorConfig {

    struct Config {
        consensus_pubkey: bytearray,
        network_identity_pubkey: bytearray,
        network_signing_pubkey: bytearray,
    }

    // A current or prospective validator should publish one of these under their address
    resource T {
        config: Self.Config,
    }

    // Returns true if addr has a published ValidatorConfig.T resource
    public has(addr: address): bool {
        return exists<T>(move(addr));
    }

    // The following are public accessors for retrieving config information about Validators

    // Retrieve a read-only instance of a specific accounts ValidatorConfig.T.config
    public config(addr: address): Self.Config acquires T {
        let t_ref: &Self.T;

        t_ref = borrow_global<T>(move(addr));
        return *&move(t_ref).config;
    }

    // Public accessor for consensus_pubkey
    public consensus_pubkey(config_ref: &Self.Config): bytearray {
        return *&move(config_ref).consensus_pubkey;
    }

    // Public accessor for network_identity_pubkey
    public network_identity_pubkey(config_ref: &Self.Config): bytearray {
        return *&move(config_ref).network_identity_pubkey;
    }

    // Public accessor for network_signing_pubkey
    public network_signing_pubkey(config_ref: &Self.Config): bytearray {
        return *&move(config_ref).network_signing_pubkey;
    }

    // The following are self methods for initializing and maintaining a Validator's config

    // Register the transaction sender as a candidate validator by creating a ValidatorConfig
    // resource under their account
    public register_candidate_validator(
        network_signing_pubkey: bytearray,
        network_identity_pubkey: bytearray,
        consensus_pubkey: bytearray) {

        move_to_sender<T>(
            T {
                config: Config {
                    consensus_pubkey: move(consensus_pubkey),
                    network_identity_pubkey: move(network_identity_pubkey),
                    network_signing_pubkey: move(network_signing_pubkey),
                }
            }
        );

        return;
    }

    // Rotate a validator candidate's consensus public key. The change will not take effect until
    // the next reconfiguration.
    public rotate_consensus_pubkey(consensus_pubkey: bytearray) acquires T {
        let t_ref: &mut Self.T;
        let config_ref: &mut Self.Config;
        let key_ref: &mut bytearray;

        t_ref = borrow_global_mut<T>(get_txn_sender());
        config_ref = &mut move(t_ref).config;
        key_ref = &mut move(config_ref).consensus_pubkey;
        *move(key_ref) = move(consensus_pubkey);

        return;
    }

}
