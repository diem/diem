module ValidatorConfig {

    // TODO(philiphayes): We should probably enforce a max length for these fields

    struct Config {
        consensus_pubkey: vector<u8>,
        validator_network_signing_pubkey: vector<u8>,
        validator_network_identity_pubkey: vector<u8>,
        validator_network_address: vector<u8>,
        fullnodes_network_identity_pubkey: vector<u8>,
        fullnodes_network_address: vector<u8>,
    }

    // A current or prospective validator should publish one of these under their address
    resource T {
        config: Self.Config,
    }

    // Returns true if addr has a published ValidatorConfig.T resource
    public has(addr: address): bool
      ensures RET == global_exists<Self.T>(addr)
    {
        return exists<T>(move(addr));
    }

    // The following are public accessors for retrieving config information about Validators

    // Retrieve a read-only instance of a specific accounts ValidatorConfig.T.config
    public config(addr: address): Self.Config acquires T
      aborts_if !global_exists<Self.T>(addr)
      ensures RET == global<Self.T>(addr).config
    {
        let t_ref: &Self.T;

        t_ref = borrow_global<T>(move(addr));
        return *&move(t_ref).config;
    }

    // Public accessor for consensus_pubkey
    public consensus_pubkey(config_ref: &Self.Config): vector<u8>
      ensures RET == config_ref.consensus_pubkey
    {
        return *&move(config_ref).consensus_pubkey;
    }

    // Public accessor for validator_network_signing_pubkey
    public validator_network_signing_pubkey(config_ref: &Self.Config): vector<u8>
      ensures RET == config_ref.validator_network_signing_pubkey
    {
        return *&move(config_ref).validator_network_signing_pubkey;
    }

    // Public accessor for validator_network_identity_pubkey
    public validator_network_identity_pubkey(config_ref: &Self.Config): vector<u8>
      ensures RET == config_ref.validator_network_identity_pubkey
    {
        return *&move(config_ref).validator_network_identity_pubkey;
    }

    // Public accessor for validator_network_address
    public validator_network_address(config_ref: &Self.Config): vector<u8>
      ensures RET == config_ref.validator_network_address
    {
        return *&move(config_ref).validator_network_address;
    }

    // Public accessor for fullnodes_network_identity_pubkey
    public fullnodes_network_identity_pubkey(config_ref: &Self.Config): vector<u8>
      ensures RET == config_ref.fullnodes_network_identity_pubkey
    {
        return *&move(config_ref).fullnodes_network_identity_pubkey;
    }

    // Public accessor for fullnodes_network_address
    public fullnodes_network_address(config_ref: &Self.Config): vector<u8>
      ensures RET == config_ref.fullnodes_network_address
    {
        return *&move(config_ref).fullnodes_network_address;
    }

    // The following are self methods for initializing and maintaining a Validator's config

    // Register the transaction sender as a candidate validator by creating a ValidatorConfig
    // resource under their account
    public register_candidate_validator(
        consensus_pubkey: vector<u8>,
        validator_network_signing_pubkey: vector<u8>,
        validator_network_identity_pubkey: vector<u8>,
        validator_network_address: vector<u8>,
        fullnodes_network_identity_pubkey: vector<u8>,
        fullnodes_network_address: vector<u8>)
        aborts_if global_exists<Self.T>(txn_sender)
        {

        move_to_sender<T>(
            T {
                config: Config {
                    consensus_pubkey: move(consensus_pubkey),
                    validator_network_signing_pubkey: move(validator_network_signing_pubkey),
                    validator_network_identity_pubkey: move(validator_network_identity_pubkey),
                    validator_network_address: move(validator_network_address),
                    fullnodes_network_identity_pubkey: move(fullnodes_network_identity_pubkey),
                    fullnodes_network_address: move(fullnodes_network_address),
                }
            }
        );

        return;
    }

    // Rotate a validator candidate's consensus public key. The change will not take effect until
    // the next reconfiguration.
    public rotate_consensus_pubkey(consensus_pubkey: vector<u8>) acquires T
        aborts_if !global_exists<Self.T>(txn_sender)
        ensures global<Self.T>(txn_sender).config.consensus_pubkey == consensus_pubkey
    {
        let t_ref: &mut Self.T;
        let config_ref: &mut Self.Config;
        let key_ref: &mut vector<u8>;

        t_ref = borrow_global_mut<T>(get_txn_sender());
        config_ref = &mut move(t_ref).config;
        key_ref = &mut move(config_ref).consensus_pubkey;
        *move(key_ref) = move(consensus_pubkey);

        return;
    }

    // TODO(philiphayes): fill out the rest of the rotate methods

    // Rotate the network public key for validator discovery. This change will be
    // committed in the next reconfiguration.
    public rotate_validator_network_identity_pubkey(
        validator_network_identity_pubkey: vector<u8>) acquires T
        aborts_if !global_exists<Self.T>(txn_sender)
        ensures global<Self.T>(txn_sender).config.validator_network_identity_pubkey
                    == validator_network_identity_pubkey
    {

        let t_ref: &mut Self.T;
        let config_ref: &mut Self.Config;
        let key_ref: &mut vector<u8>;

        t_ref = borrow_global_mut<T>(get_txn_sender());
        config_ref = &mut move(t_ref).config;
        key_ref = &mut move(config_ref).validator_network_identity_pubkey;
        *move(key_ref) = move(validator_network_identity_pubkey);

        return;
    }

    // Rotate the network address for validator discovery. This change will be
    // committed in the next reconfiguration.
    public rotate_validator_network_address(
        validator_network_address: vector<u8>) acquires T
        aborts_if !global_exists<Self.T>(txn_sender)
        ensures global<Self.T>(txn_sender).config.validator_network_address
                    == validator_network_address
        {

        let t_ref: &mut Self.T;
        let config_ref: &mut Self.Config;
        let key_ref: &mut vector<u8>;

        t_ref = borrow_global_mut<T>(get_txn_sender());
        config_ref = &mut move(t_ref).config;
        key_ref = &mut move(config_ref).validator_network_address;
        *move(key_ref) = move(validator_network_address);

        return;
    }

}
